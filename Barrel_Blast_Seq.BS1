' =========================================================================
'
'   File....... Barrel_Blast_Seq.BS1
'   Purpose.... "Barrel Blaster" Sequence Engine
'   Author..... Jon Williams -- Parallax, Inc.
'   E-mail..... jwilliams@parallax.com
'   Started....
'   Updated.... 07 JUN 2005
'
'   {$STAMP BS1}
'   {$PBASIC 1.0}
'
' =========================================================================


' -----[ Program Description ]---------------------------------------------'
'
' Based on Brent Ross's (Devious Concoctions) Barrel Blaster program


' -----[ Revision History ]------------------------------------------------


' -----[ I/O Definitions ]-------------------------------------------------

SYMBOL  Trigger         = PIN7                  ' from trigger device


' -----[ Constants ]-------------------------------------------------------

SYMBOL  Yes             = 1                     ' for active-high input
SYMBOL  No              = 0

SYMBOL  DelayRes        = 100                   ' 100 ms delay resolution


' -----[ Variables ]-------------------------------------------------------

SYMBOL  pntr            = B0                    ' record pointer
SYMBOL  ctrl            = B1                    ' control outputs
SYMBOL  timer           = B2                    ' step timer ( x DelayRes )


' -----[ EEPROM Data ]-----------------------------------------------------

' Sequence Database
' -- each entry has two bytes: outputs, timing (1 - 255 units x DelayRes)
'
' Outputs:
' %x0000111
'       |||
'       ||+-- Fogger
'       |+--- Pneumatics
'       +---- Cannon

Barrel_Blaster:
  EEPROM (%00000001,  20)                       ' fogger = 2 secs
  EEPROM (%00000000,   5)                       ' off = 0.5 secs
  EEPROM (%00000010,  25)                       ' pneumatics = 2.5 secs
  EEPROM (%00000110,  10)                       ' cannon = 1 sec
  EEPROM (%00000010,   5)                       ' cannon off = 0.5 secs
  EEPROM (%00000000, 250)                       ' all off = 25 secs
  EEPROM (%00000000, 250)                       ' all off = 25 secs
  EEPROM (%00000000, 200)                       ' all off = 20 secs
  EEPROM (%00000000, 200)                       ' all off = 20 secs
  EEPROM (%00000000,   0)                       ' reset


' -----[ Initialization ]--------------------------------------------------

Reset:
  DIRS = %00000111                              ' fog, pneumatics, cannon


' -----[ Program Code ]----------------------------------------------------

Main:
  IF Trigger = No THEN Main                     ' wait for trigger

Sequencer:
  pntr = 0                                      ' reset record pointer

Get_Record:
  READ pntr, ctrl                               ' read outputs
  pntr = pntr + 1                               ' point to timing byte
  READ pntr, timer                              ' read timing byte
  pntr = pntr + 1                               ' point to next record
  IF timer = 0 THEN Main                        ' test for end

Update_Outputs:
  PINS = ctrl                                   ' refresh output pins

Delay:
  PAUSE DelayRes                                ' wait
  timer = timer - 1                             ' update timer
  IF timer = 0 THEN Get_Record                  ' expired?
  GOTO Delay                                    ' -- no, keep timing

  END


' -----[ Subroutines ]-----------------------------------------------------